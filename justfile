#!/usr/bin/env just

# â–°â–°â–° Settings â–°â–°â–° #
set shell := ["bash", "-euo", "pipefail", "-c"]
set windows-shell := ["C:/Program Files/Git/usr/bin/bash.exe", "-euo", "pipefail", "-c"]
set dotenv-load := true
set allow-duplicate-recipes := true

# â–°â–°â–° Variables â–°â–°â–° #
project_root    := justfile_directory()
output_directory := project_root + "/dist"

system := `rustc --version --verbose |  grep '^host:' | awk '{print $2}'`
target_dir      := project_root + "/target"
main_package      := "lic"
main_bin_flag     := "--bin" + main_package
spdx_parser_pkg := "spdx_parser"

py_script_dir := project_root + "/scripts/parse_comments"
py_script     := py_script_dir + "/main.py"
json_output_rel := "../../lic/src/comment-tokens.json"
json_output     := py_script_dir + "/" + json_output_rel

release_flag   := "--release"
workspace_flag := "--workspace"
all_flag       := "--all"
verbose_flag   := "-vv"

default:
    just --list {{justfile()}}

# â–°â–°â–° Build & Check â–°â–°â–° #

check:
    @echo "ðŸ”Ž Checking workspace..."
    cargo check {{workspace_flag}}

check-release: 
    @echo "ðŸ”Ž Checking workspace (release)..."
    cargo check {{workspace_flag}} {{release_flag}}

build target="aarch64-apple-darwin" package=(main_package):
    @echo "ðŸ”¨ Building workspace (debug)..."
    cargo build {{workspace_flag}} --bin {{package}} --target {{target}}

download-templates:
    git init
    git remote add origin https://github.com/spdx/license-list-data.git
    git config core.sparseCheckout true
    echo "template/" >> .git/info/sparse-checkout
    git pull origin main

download-languages:
    curl -f -L -O -X GET https://github.com/philocalyst/lang-config/releases/latest/download/languages.json
    mv languages.json /Users/philocalyst/Projects/lichen/lic/assets/comment-tokens.json

create-notes raw_tag outfile changelog:
    #!/usr/bin/env bash
    
    tag_v="{{raw_tag}}"
    tag="${tag_v#v}" # Remove prefix v

    # Changes header for release notes
    printf "# What's new\n" > "{{outfile}}"

    if [[ ! -f "{{changelog}}" ]]; then
      echo "Error: {{changelog}} not found." >&2
      exit 1
    fi

    echo "Extracting notes for tag: {{raw_tag}} (searching for section [$tag])"
    # Use awk to extract the relevant section from the changelog
    awk -v tag="$tag" '
      # start printing when we see "## [<tag>]" (escape brackets for regex)
      $0 ~ ("^## \\[" tag "\\]") { printing = 1; next }
      # stop as soon as we hit the next "## [" section header
      printing && /^## \[/       { exit }
      # otherwise, if printing is enabled, print the current line
      printing                    { print }

      # Error handling
      END {
        if (found_section != 0) {
          # Print error to stderr
          print "Error: awk could not find section header ## [" tag "] in " changelog_file > "/dev/stderr"
          exit 1
        }
      }
    ' "{{changelog}}" >> "{{outfile}}"

    # Check if the output file has content
    if [[ -s {{outfile}} ]]; then
      echo "Successfully extracted release notes to '{{outfile}}'."
    else
      # Output a warning if no notes were found for the tag
      echo "Warning: '{{outfile}}' is empty. Is '## [$tag]' present in '{{changelog}}'?" >&2
    fi

build-release target=(system) package=(main_package):
    @echo "ðŸš€ Building workspace (release) for {{target}}â€¦"
    cargo build {{workspace_flag}} {{release_flag}} --bin {{package}} --target {{target}}

package target=(system):
    just build-release {{target}}
    @echo "ðŸ“¦ Packaging release binaryâ€¦"
    @mkdir -p dist

    @bash -euo pipefail -c '\
      ext=""; \
      if [[ "{{target}}" == *windows-msvc ]]; then \
        ext=".exe"; \
      fi; \
      bin="target/{{target}}/release/{{main_package}}${ext}"; \
      out="dist/{{main_package}}-{{target}}${ext}"; \
      \
      echo " - cp $bin â†’ $out"; \
      cp "$bin" "$out"; \
      \
    '


checksum directory=(output_directory):
    @echo "ðŸ”’ Creating checksums..."
    @find "{{directory}}" -type f \
    ! -name "checksums.sha256" \
    ! -name "*.sha256" \
    -exec sh -c 'sha256sum "$1" > "$1.sha256"' _ {} \;
    @echo "âœ… Checksums created!"

[no-cd]
compress-binaries target_directory=("."): 
    #!/usr/bin/env bash
    
    find "{{target_directory}}" -maxdepth 1 -type f -print0 | while IFS= read -r -d $'\0' file; do
    # Check if the file command output indicates a binary/executable type
    if file "$file" | grep -q -E 'executable|ELF|Mach-O|shared object'; then
        # Get the base filename without the path
        filename=$(basename "$file")
        
        # Get the base name without version number
        basename="${filename%%-*}"
        
        echo "Archiving binary file: $filename, with internal file $basename"
        
        # Create archive with just the basename, no directory structure
        tar -czf "${file}.tar.gz" \
            -C "$(dirname "$file")" \
            -s "|^${filename}$|${basename}|" \
            "$(basename "$file")"
    fi
    done


# â–°â–°â–° Run â–°â–°â–° #

run package=(main_package) +args="":
    @echo "â–¶ï¸ Running {{package}} (debug)..."
    cargo run --bin {{package}} -- {{args}}

run-release package=(main_package) +args="":
    @echo "â–¶ï¸ Running {{package}} (release)..."
    cargo run --bin {{package}} {{release_flag}} -- {{args}}

run-example-spdx: 
    @echo "â–¶ï¸ Running spdx_parser example (basic_conversion)..."
    cargo run --bin {{spdx_parser_pkg}} --example basic_conversion

run-example-spdx-release:
    @echo "â–¶ï¸ Running spdx_parser example (basic_conversion, release)..."
    cargo run --bin {{spdx_parser_pkg}} {{release_flag}} --example basic_conversion

# â–°â–°â–° Code Generation â–°â–°â–°

generate-comments:
    @echo "ðŸ”§ Generating commentâ€tokens JSON..."
    @mkdir -p "{{py_script_dir}}"
    @uv run "{{py_script}}" > "{{json_output}}"

# â–°â–°â–° Test â–°â–°â–°

test: 
    @echo "ðŸ§ª Running workspace tests..."
    cargo test {{workspace_flag}}

test-with +args: 
    @echo "ðŸ§ª Running workspace tests with args: {{args}}"
    cargo test {{workspace_flag}} -- {{args}}

# â–°â–°â–° Format & Lint â–°â–°â–°

fmt:
    @echo "ðŸ’… Formatting Rust code..."
    cargo fmt {{all_flag}}

fmt-check:
    @echo "ðŸ’… Checking Rust code formatting..."
    cargo fmt {{all_flag}} -- --check

lint:
    @echo "ðŸ§¹ Linting with Clippy (debug)..."
    cargo clippy {{workspace_flag}} -- -D warnings

lint-release:
    @echo "ðŸ§¹ Linting with Clippy (release)..."
    cargo clippy {{workspace_flag}} {{release_flag}} -- -D warnings

lint-fix:
    @echo "ðŸ©¹ Fixing Clippy lints..."
    cargo clippy {{workspace_flag}} --fix --allow-dirty --allow-staged

# â–°â–°â–° Documentation â–°â–°â–° #

doc:
    @echo "ðŸ“š Generating documentation..."
    cargo doc {{workspace_flag}} --no-deps

doc-open: doc
    @echo "ðŸ“š Opening documentation in browser..."
    cargo doc {{workspace_flag}} --no-deps --open

# â–°â–°â–° Cleaning â–°â–°â–° #

clean:
    @echo "ðŸ§¹ Cleaning build artifacts..."
    cargo clean

clean-all: clean
    @echo "ðŸ§¹ Removing generated JSON file..."
    rm -f "{{json_output}}"
    @echo "ðŸ§¹ Cleaning Python virtual environment..."
    cd "{{py_script_dir}}" && rm -rf .venv
    @echo "ðŸ§¹ Cleaning Python cache..."
    cd "{{py_script_dir}}" && rm -rf .uv_cache __pycache__

# â–°â–°â–° Installation & Update â–°â–°â–° #

update:
    @echo "ðŸ”„ Updating dependencies..."
    cargo update

release: build-release
    just checksum
    
install package=(main_package): build-release 
    @echo "ðŸ’¾ Installing {{main_package}} binary..."
    cargo install --bin {{package}}

install-force package=(main_package): build-release
    @echo "ðŸ’¾ Force installing {{main_package}} binary..."
    cargo install --bin {{package}} --force

# â–°â–°â–° Aliases â–°â–°â–° #

alias b    := build
alias br   := build-release
alias c    := check
alias cr   := check-release
alias t    := test
alias f    := fmt
alias l    := lint
alias lr   := lint-release
alias lf   := lint-fix
alias cl   := clean
alias cla  := clean-all
alias up   := update
alias i    := install
alias ifo  := install-force
alias rr   := run-release
alias rre  := run-example-spdx-release
alias gc   := generate-comments
